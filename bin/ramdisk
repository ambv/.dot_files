#!/usr/bin/env fish

set CPYTHON ~/Documents/Python/cpython.git
set RAMDISK /Volumes/RAMDisk
set WORKTREE $RAMDISK/cpython
set BRANCH "main"
set ALTBRANCH "3.10"

if test -d $RAMDISK 
    echo "RAM disk already exists at $RAMDISK. Unmount by running:"
    echo
    echo "    hdiutil eject $RAMDISK"
    echo
    exit 1
end

set SUDO_PROMPT 1
if sudo -nv 2>/dev/null
    # sudo timestamp is fresh before this script ran
    set SUDO_PROMPT 0
else
    echo "This script requires admin privileges to set ownership properly."
    sudo -v
end

cd $CPYTHON
git worktree list --porcelain | grep "^worktree $RAMDISK" | gsed -e "s/worktree //" | while read stale_worktree
    echo "Stale cpython worktree found in $stale_worktree, deleting."
    git worktree remove $stale_worktree
end

set DISK_ID (hdiutil attach -nobrowse -nomount ram://16777216 | string trim)
echo "Creating a RAM disk at $DISK_ID..."
echo -n "Waiting for it to initialize..."

while not diskutil list $DISK_ID >/dev/null 2>/dev/null
    echo -n . 
    sleep 1
end

echo " done!"

echo "Formatting the RAM disk..."
diskutil erasevolume HFS+ 'RAMDisk' $DISK_ID 

# if string match -qr "^$BRANCH\$" (git branch --show-current)
#    echo "$BRANCH checked out in $CPYTHON, using $ALTBRANCH instead."
#    set BRANCH $ALTBRANCH
# end

git worktree add $WORKTREE $BRANCH

sudo diskutil enableOwnership $DISK_ID
sudo chown ambv:staff $RAMDISK
if test $SUDO_PROMPT -eq 1
    sudo -k
end

echo "All done. Switch to the RAM disk checkout by running:"
echo
echo "    cd $WORKTREE"
echo